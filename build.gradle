buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'

        //  Dependencies for pushing to jcenter
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
     }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//  Versions are x.y.z and version code is xxyyzz.  Leading 0s are stripped.
version = "0.2.0"
def versionNum = 200

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    resourcePrefix "andele__"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode versionNum
        versionName version
    }

}

def siteUrl = 'https://github.com/hiqes/andele'
def gitUrl = 'https://github.com/hiqes/andele.git'

group = 'com.hiqes.andele'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'andele'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "andele"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}

allprojects {
    repositories {
        jcenter()
    }
}
